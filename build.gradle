buildscript {
    ext.kotlin_version = '1.1.0-beta-38'
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jacoco'

defaultTasks 'run'

mainClassName = 'pl.michalkowol.BootKt'
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar) {
    baseName = "${project.name}-assembly"
    manifest = jar.manifest
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task stage(dependsOn: ['fatJar', 'build', 'clean'])
build.mustRunAfter clean
fatJar.mustRunAfter build
stage.dependsOn(fatJar)

sourceSets {
    main.java.srcDirs += "$buildDir/generated/source/kapt/main"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
    dependsOn test
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    maven { url 'http://dl.bintray.com/kotlin/kotlinx.coroutines' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    ext.kotlinxcoroutines = '0.8-beta'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.1.+'

    compile 'com.sparkjava:spark-core:2.5.+'

    compile 'com.github.softwareberg:database:0.1.+'
    compile 'com.github.softwareberg:httpclient:0.1.+'

    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-nio:$kotlinxcoroutines"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-rx:$kotlinxcoroutines"

    testCompile 'junit:junit:4.12'
//    testCompile 'com.nhaarman:mockito-kotlin:1.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
